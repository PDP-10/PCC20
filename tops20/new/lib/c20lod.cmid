;
;	c20lod - load-time initialization (tops20)
;

title c20lod
.insrt c:minsrt

.global timing,pzero,pushd

.idata
;	no literals in this code!

mdata	linit

	movei	p,$argv"	; temporary pdl

mdata	istart

	jfcl

	; setup segment boundaries

	hlrz	a,20
	movem	a,seg0lo
	hrrz	a,20
	movem	a,seg0hi
	setzm	20
	hlrz	a,21
	movem	a,seg1lo
	hrrz	a,21
	movem	a,seg1hi
	setzm	21
	hlrz	a,22
	movem	a,seg2lo
	hrrz	a,22
	movem	a,seg2hi
	setzm	22
	hlrz	a,23
	movem	a,seg3lo
	hrrz	a,23

	hrrz	b,23+1		; on tops-20, the symbol table
				; is written as segment 4.  at run time,
				; however, we prefer to consider the
				; symbol table to be part of segment 3.
	caml	b,a		; in case the symbol table is written other
				; than where we expect (possible in the case
				; of a shared library), then make sure that
				; seg3hi is not decreased.
	 move	a,b
	movem	a,seg3hi
	setzm	23
	setzm	24

	jfcl
	setzb	a,0
	setzb	b,c		; clean up
	setz	d,		; like a good boy should
	haltf			; return to loader

; these variables contain the segment boundaries in the dumped program

.idata
mdata	seg0lo
	0
mdata	seg0hi
	0
mdata	seg1lo
	0
mdata	seg1hi
	0
mdata	seg2lo
	0
mdata	seg2hi
	0
mdata	seg3lo
	0
mdata	seg3hi
	0

.code
end
