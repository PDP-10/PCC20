;
;	C20FO - TOPS-20 FAST CHARACTER OUTPUT ROUTINES
;

TITLE C20FO
.INSRT <C>NC
.INSRT <C>NM

BLKSIZ==200
BLKCNT==5*BLKSIZ
NL==12
CR==15

;	SIOT STUFF

.UDATA
SBUF:	BLOCK	BLKSIZ		; BUFFER
SPTR:	BLOCK	1		; POINTER TO NEXT CHARACTER POSITION
SCHN:	BLOCK	1		; JFN
SCNT:	BLOCK	1		; COUNT OF POSITIONS LEFT IN BUFFER
.IDATA
SBPT:	440700,,SBUF		; POINTER TO BEGINNING OF BUFFER
.CODE

CENTRY	OOPN,[NAME]
	CALL	COPEN,[NAME,[["w]],0]
	JUMPL	A,OP$RET	; -1 MEANS OPEN FAILED
	MOVE	B,(A)		; GET JFN
	MOVEM	B,SCHN		; SAVE IT
	MOVEI	B,BLKCNT	; SET UP COUNT
	MOVEM	B,SCNT
	MOVE	B,SBPT		; SET UP POINTER
	MOVEM	B,SPTR
OP$RET:	RETURN

CENTRY	OFLS			; FLUSH BUFFER
	MOVEI	C,BLKCNT
	SUB	C,SCNT
	JUMPLE	C,FL$RET
	MOVE	A,SCHN
	MOVE	B,SBPT
	MOVN	C,C
	SOUT
FL$RET:	MOVE	D,SBPT
	MOVEM	D,SPTR
	MOVEI	D,BLKCNT
	MOVEM	D,SCNT
	RETURN

CENTRY	OUTI,[CC]		; OUTPUT IMAGE CHARACTER
	MOVE	A,CC
	IDPB	A,SPTR
	SOSG	SCNT
	 CALL	OFLS
	RETURN

CENTRY	OUTC,[CC]		; OUTPUT ASCII CHARACTER
	MOVE	A,CC
	CAIN	A,NL
	 GO	OC$NL
OC$1:	IDPB	A,SPTR
	SOSG	SCNT
	 CALL	OFLS
	RETURN
OC$NL:	MOVEI	A,CR
	IDPB	A,SPTR
	SOSG	SCNT
	 CALL	OFLS
	MOVEI	A,NL
	GO	OC$1

CENTRY	OUTS,[STR]		; OUTPUT ASCII STRING
	MOVE	B,STR
OS$2:	SKIPN	A,(B)
	 GO OS$RET
	ADDI	B,1
	CAIN	A,NL
	 GO	OS$NL
OS$1:	IDPB	A,SPTR
	SOSG	SCNT
	 CALL	OFLS
	GO	OS$2
OS$NL:	MOVEI	A,CR
	IDPB	A,SPTR
	SOSG	SCNT
	 CALL	OFLS
	MOVEI	A,NL
	GO	OS$1
OS$RET:	RETURN

CENTRY	OCLS			; CLOSE FILE
	CALL	OFLS
	MOVE	A,SCHN
	CLOSF
	 JFCL
	RETURN

END
