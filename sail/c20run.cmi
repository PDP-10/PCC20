;
;	C20RUN - BASIC C RUN-TIME SUPPORT (TOPS20)
;

TITLE C20RUN
.INSRT <C>NC
.INSRT <C>NM

.GLOBAL A,B,C,D,P,GO,.CCALL,.VCALL,.ACALL,.XCALL
.GLOBAL ARGC,ARGV,SEG3HI,FNWORDS

PDLSIZ==20000				; DESIRED PDL SIZE

;
;	START-UP ROUTINE
;

IENTRY	BEG				; START FROM DDT

	MOVE	A,[440700,,[0]]		; NULL STRING
	RSCAN				; SET RSCAN BUFFER TO NULL

IENTRY	START

	RESET

	;SET UP UUO HANDLER

	MOVE	A,[JSR UUOH"]
	MOVEM	A,41

	;SET UP PDLOV HANDLER

	MOVEI	1,400000
	MOVE	2,[LEVTAB,,CHNTAB]
	SIR
	MOVEI	1,400000
	EIR
	MOVEI	1,400000
	MOVE	2,[000400,,000000]
	AIC

	MOVE	A,FNWORDS
	JUMPN	A,R3			; RESTARTING, DON'T REINITIALIZE
					; FREE STORAGE SYSTEM
	MOVE	P,PDLBOT		; STACK
	MOVE	A,SEG3HI
	ADDI	A,1			; FIRST FREE LOCATION
	HRLZI	B,1			; 1000000
	SUB	B,A			; NUMBER OF FREE WORDS
	MOVEM	B,FNWORDS		; -> NUMBER OF WORDS ALLOCATED
	CAIL	B,400000
	 GO	R2			; AFRET DOESN'T LIKE BIG BLOCKS
R1:	CALL	AFRET,[A,B]		; ADD TO FREE LIST
	GO	R3

R2:	SUBI	B,377777
	PUSH	P,A
	PUSH	P,B
	CALL	AFRET,[A,B]
	POP	P,B
	POP	P,A
	ADDI	A,1(B)			; DON'T ALLOW BLOCKS TO MERGE
	MOVEI	B,377776
	GO	R1

R3:	MOVE	P,PDLBOT		; STACK
	MCALL	$SETUP

IENTRY	RESTART
	MOVE	P,PDLBOT
	CALL	MAIN,[ARGC,[[ARGV]]]
	CALL	CEXIT,[[[0]]]

IENTRY	.EXIT

	MOVE	C,A	; SAVE RETURN CODE
	SETO	A,
	CLOSF		; CLOSE ALL FILES
	 JFCL
	MOVE	A,C
	HALTF		; COMMIT SUICIDE
	GO	.-1	; DON'T ALLOW RESTARTING

IENTRY	PDLSRV
	DEBRK


;
;	EXIT ROUTINES
;

CENTRY	CEXIT,[CC]
	CALL	CLOSALL	; CLOSE ALL C FILES
	MOVE	A,CC
	GO	.EXIT

CENTRY	CQUIT,[CC]
	CALL	CEXIT,[CC]


IENTRY	FIXIFY

	JUMPL	A,FIXL
	FADR	A,[.499999]
	UFA	A,[233000000000']
	TLZ	B,777000'
	JRST	@0
FIXL:	MOVN	A,A
	FADR	A,[.499999]
	UFA	A,[233000000000']
	TLZ	B,777000'
	MOVN	B,B
	JRST	@0

;	IMPURE AREA

.UDATA
MDATA	TIMING			; TIMING FLAG
	BLOCK	1
MDATA	EXITER
	BLOCK	1		; EXIT ROUTINE (FOR TIMING)
PDL:	BLOCK	PDLSIZ		; THE STACK
.IDATA
MDATA	CHNTAB
	BLOCK	9.
	3,,PDLSRV		; PDLOV HANDLER
	BLOCK	26.
MDATA	LEVTAB
	BLOCK	2
	PCLEV3
MDATA	PCLEV3
	BLOCK	1

MDATA	PDLBOT
	PDL
MDATA	PDLTOP
	PDL+PDLSIZ-1
.PDATA
MDATA	PZERO
	PUSH	P,ZERO
MDATA	ZERO
	0
MDATA	PUSHD
	PUSH	P,D

CONSTANTS

MDATA	PATCH

END	START
